import glob
file_list = glob.glob('*.cpp')
file_list.remove('lua-engine.cpp') # use logic below for this

subdirs = Split("""
boards
drivers/common
fir
input
utils
""")
#palettes

Import('env')
Export('env')

if env['EMSCRIPTEN']:
  file_list.remove('netplay.cpp')
  file_list.remove('movie.cpp')
  file_list.remove('cheat.cpp')
  file_list.remove('asm.cpp')

if env['LUA']:
  file_list.append('lua-engine.cpp')
  if env['SYSTEM_LUA'] == 0:
    subdirs.append('lua')

if env['CREATE_AVI']:
  subdirs.append('drivers/videolog')



for dir in subdirs:
  subdir_files = SConscript('%s/SConscript' % dir)
  file_list.append(subdir_files)
if env['PLATFORM'] == 'win32':
  platform_files = SConscript('drivers/win/SConscript')
elif env['EMSCRIPTEN']:
  platform_files = SConscript('drivers/em/SConscript')
else:
  platform_files = SConscript('drivers/sdl/SConscript')
file_list.append(platform_files)

print(env['LINKFLAGS'])

if env['PLATFORM'] == 'win32':
  fceux = env.Program('fceux.exe', file_list)
elif env['EMSCRIPTEN']:
  target = '../dist/fceux'
  fceux = env.Program(target, file_list)
else:
  fceux = env.Program('fceux', file_list)
Return('fceux')
